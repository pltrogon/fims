//**********************************************************************
// DEFINING THE FIMS GEOMETRY AS MINIMUM CELL
//     Requires mirroring in x and y
//**********************************************************************

// Import values from run control file.
// Parameters in micrometers
Include "../runControl";

// Finite element sizes for different regions
//gridElm: points on the corner of the grid without a hole (x4)
//holeElm: points on the hole of the grid (x12)
//padElm: points along the edge and center of the pad (x16)
//diElm: points on the corner of the SiO2 without a pad (x4)
//cathElm: points along the cathode far above the grid (x4)

gridElm = (pitch - holeRadius)/5;
holeElm = gridThickness;
padElm = thicknessSiO2;
diElm = padLength/6;
cathElm = pitch/10;

// Derived Z-coordinates for different layers
//     Center of grid: z = 0
halfPitch = pitch/2;
cathodeDistance = gridThickness/2 + cathodeHeight;
gridTop = gridThickness/2;
gridBottom = -gridThickness/2;
topSiO2 = - gridThickness/2 - gridStandoff + thicknessSiO2;
padBase = -gridThickness/2 - gridStandoff;
pillarRadius = 20;

//Some pad parameters
halfPad = padLength/2;
padMath = (3^.5)*halfPad;

// Global coordinate limits
xMin = 0;
xMax = (3^.5)*halfPitch;
yMin = 0;
yMax = halfPitch;

//**********************************************************************
// PILLAR STRUCTURE
//**********************************************************************
// Values left in 900s to allow for easier review before being finalized

// Points - base of the pillars
Point(901) = {2*xMax/3, 0, topSiO2, diElm};
Point(902) = {2*xMax/3 + pillarRadius, 0, topSiO2, diElm};
Point(903) = {2*xMax/3 - pillarRadius, 0, topSiO2, diElm};
Point(904) = {2*xMax/3, pillarRadius, topSiO2, diElm};

Point(905) = {xMax/3, yMax, topSiO2, diElm};
Point(906) = {xMax/3 + pillarRadius, yMax, topSiO2, diElm};
Point(907) = {xMax/3 - pillarRadius, yMax, topSiO2, diElm};
Point(908) = {xMax/3, yMax - pillarRadius, topSiO2, diElm};

// Points - top of the pillars
Point(909) = {2*xMax/3, 0, gridBottom, diElm};
Point(910) = {2*xMax/3 + pillarRadius, 0, gridBottom, diElm};
Point(911) = {2*xMax/3 - pillarRadius, 0, gridBottom, diElm};
Point(912) = {2*xMax/3, pillarRadius, gridBottom, diElm};

Point(913) = {xMax/3, yMax, gridBottom, diElm};
Point(914) = {xMax/3 + pillarRadius, yMax, gridBottom, diElm};
Point(915) = {xMax/3 - pillarRadius, yMax, gridBottom, diElm};
Point(916) = {xMax/3, yMax - pillarRadius, gridBottom, diElm};

// Lines - base of pillars
Line(901) = {903, 902};
Circle(902) = {902, 901, 904};
Circle(903) = {904, 901, 903};

Line(904) = {906, 907};
Circle(905) = {907, 905, 908};
Circle(906) = {908, 905, 906};

// Lines - top of pillars
Line(907) = {911, 910};
Circle(908) = {910, 909, 912};
Circle(909) = {912, 909, 911};

Line(910) = {914, 915};
Circle(911) = {915, 913, 916};
Circle(912) = {916, 913, 914};

// Lines - connecting top to base of pillars
Line(913) = {910, 902};
Line(914) = {911, 903};
Line(915) = {912, 904};

Line(916) = {914, 906};
Line(917) = {916, 908};
Line(918) = {915, 907};

// Surfaces - base of pillars
Curve Loop(901) = {901, 902, 903};
Plane Surface(901) = {901};
Curve Loop(902) = {904, 905, 906};
Plane Surface(902) = {902};

// Surfaces - top of pillars
Curve Loop(903) = {907, 908, 909};
Plane Surface(903) = {903};
Curve Loop(904) = {910, 911, 912};
Plane Surface(904) = {904};

// Curves - connecting top to base of pillars
Curve Loop(905) = {907, 913, -901, -914};
Surface(905) = {905};
Curve Loop(906) = {908, 915, -902, -913};
Surface(906) = {906};
Curve Loop(907) = {909, 914, -903, -915};
Surface(907) = {907};

Curve Loop(908) = {910, 918, -904, -916};
Surface(908) = {908};
Curve Loop(909) = {911, 917, -905, -918};
Surface(909) = {909};
Curve Loop(910) = {912, 916, -906, -917};
Surface(910) = {910};

// Volume - pillars
Surface Loop(901) = {901, 903, 905, 906, 907};
Surface Loop(902) = {902, 904, 908, 909, 910};
Volume(901) = {901};
Volume(902) = {902};


//**********************************************************************
// GRID STRUCTURE
//**********************************************************************
// Points - top of the grid, boundary
Point(1) = {0, 0, gridTop, holeElm};
Point(2) = {padLength-holeRadius, 0, gridTop, holeElm};
Point(3) = {padLength, holeRadius, gridTop, holeElm};
Point(4) = {padLength+holeRadius, 0, gridTop, holeElm};
Point(5) = {padLength, 0, gridTop, holeElm};

Point(6) = {xMax, 0, gridTop, gridElm};
Point(7) = {xMax, yMax, gridTop, holeElm};
Point(8) = {xMax - padLength+holeRadius, yMax, gridTop, holeElm};
Point(9) = {xMax - padLength, yMax-holeRadius, gridTop, holeElm};
Point(10) = {xMax - padLength-holeRadius, yMax, gridTop, holeElm};
Point(11) = {xMax - padLength, yMax, gridTop, holeElm};
Point(12) = {0, yMax, gridTop, gridElm};

//Points - top of the grid, inner holes
Point(13) = {halfPad, padMath, gridTop, holeElm};
Point(14) = {halfPad+holeRadius, padMath, gridTop, holeElm};
Point(15) = {halfPad-holeRadius, padMath, gridTop, holeElm};
Point(16) = {halfPad, padMath+holeRadius, gridTop, holeElm};
Point(17) = {halfPad, padMath-holeRadius, gridTop, holeElm};

Point(18) = {xMax - halfPad, yMax - padMath, gridTop, holeElm};
Point(19) = {xMax - halfPad+holeRadius, yMax - padMath, gridTop, holeElm};
Point(20) = {xMax - halfPad, yMax - padMath+holeRadius, gridTop, holeElm};
Point(21) = {xMax - halfPad-holeRadius, padMath, gridTop, holeElm};
Point(22) = {xMax - halfPad, yMax - padMath -holeRadius, gridTop, holeElm};

// Points - bottom of the grid boundary
Point(23) = {0, 0, gridBottom, holeElm};
Point(24) = {padLength-holeRadius, 0, gridBottom, holeElm};
Point(25) = {padLength, holeRadius, gridBottom, holeElm};
Point(26) = {padLength+holeRadius, 0, gridBottom, holeElm};
Point(27) = {padLength, 0, gridBottom, holeElm};
Point(28) = {xMax, 0, gridBottom, gridElm};

Point(29) = {xMax, yMax, gridBottom, holeElm};
Point(30) = {xMax - padLength+holeRadius, yMax, gridBottom, holeElm};
Point(31) = {xMax - padLength, yMax-holeRadius, gridBottom, holeElm};
Point(32) = {xMax - padLength-holeRadius, yMax, gridBottom, holeElm};
Point(33) = {xMax - padLength, yMax, gridBottom, holeElm};
Point(34) = {0, yMax, gridBottom, gridElm};

//Points - bottom of the grid, inner holes
Point(35) = {halfPad, padMath, gridBottom, holeElm};
Point(36) = {halfPad+holeRadius, padMath, gridBottom, holeElm};
Point(37) = {halfPad-holeRadius, padMath, gridBottom, holeElm};
Point(38) = {halfPad, padMath+holeRadius, gridBottom, holeElm};
Point(39) = {halfPad, padMath-holeRadius, gridBottom, holeElm};

Point(40) = {xMax - halfPad, yMax - padMath, gridBottom, holeElm};
Point(41) = {xMax - halfPad+holeRadius, yMax - padMath, gridBottom, holeElm};
Point(42) = {xMax - halfPad, yMax - padMath+holeRadius, gridBottom, holeElm};
Point(43) = {xMax - halfPad-holeRadius, padMath, gridBottom, holeElm};
Point(44) = {xMax - halfPad, yMax - padMath -holeRadius, gridBottom, holeElm};

// Lines - top of grid, boundary
Line(1) = {1, 2};
Circle(2) = {2, 5, 3};
Circle(3) = {3, 5, 4};
Line(4) = {4, 6};
Line(5) = {6, 7};
Line(6) = {7, 8};
Circle(7) = {8, 11, 9};
Circle(8) = {9, 11, 10};
Line(9) = {10, 12};
Line(10) = {12, 1};

// Lines - top of grid, holes
Circle(11) = {14, 13, 17};
Circle(12) = {17, 13, 15};
Circle(13) = {15, 13, 16};
Circle(14) = {16, 13, 14};
Circle(15) = {19, 18, 22};
Circle(16) = {22, 18, 21};
Circle(17) = {21, 18, 20};
Circle(18) = {20, 18, 19};

// Lines - bottom of grid, boundary
Line(19) = {23, 24};
Circle(20) = {24, 27, 25};
Circle(21) = {25, 27, 26};
Line(22) = {26, 911};
Line(919) = {910, 28};
Line(23) = {28, 29};
Line(24) = {29, 30};
Circle(25) = {30, 33, 31};
Circle(26) = {31, 33, 32};
Line(27) = {32, 914};
Line(920) = {915, 34};
Line(28) = {34, 23};

// Lines - bottom of grid, holes
Circle(29) = {36, 35, 39};
Circle(30) = {39, 35, 37};
Circle(31) = {37, 35, 38};
Circle(32) = {38, 35, 36};
Circle(33) = {41, 40, 44};
Circle(34) = {44, 40, 43};
Circle(35) = {43, 40, 42};
Circle(36) = {42, 40, 41};

// Lines - Connecting top and bottom grid
Line(37) = {1, 23};
Line(38) = {2, 24};
Line(39) = {3, 25};
Line(40) = {4, 26};
Line(41) = {6, 28};
Line(42) = {7, 29};
Line(43) = {8, 30};
Line(44) = {9, 31};
Line(45) = {10, 32};
Line(46) = {12, 34};

Line(47) = {14, 36};
Line(48) = {17, 39};
Line(49) = {15, 37};
Line(50) = {16, 38};
Line(51) = {19, 41};
Line(52) = {22, 44};
Line(53) = {21, 43};
Line(54) = {20, 42};

// Surface - top of grid
Curve Loop(1) = {11, 12, 13, 14};
Curve Loop(2) = {15, 16, 17, 18};
Curve Loop(3) = {1, 2, 3, 4, 5, 6, 7, 8, 9, 10};
Plane Surface(3) = {3, 1, 2};

// Surface - bottom of grid
Curve Loop(4) = {29, 30, 31, 32};
Curve Loop(5) = {33, 34, 35, 36};
Curve Loop(6) = {19, 20, 21, 22, -909, -908, 919, 23, 24, 25, 26, 27, -912, -911, 920, 28};
Plane Surface(6) = {6, 4, 5};

// Surface - Sides of holes in grid
Curve Loop(7) = {11, 48, -29, -47};
Surface(7) = {7};
Curve Loop(8) = {12, 49, -30, -48};
Surface(8) = {8};
Curve Loop(9) = {13, 50, -31, -49};
Surface(9) = {9};
Curve Loop(10) = {14, 47, -32, -50};
Surface(10) = {10};

Curve Loop(11) = {15, 52, -33, -51};
Surface(11) = {11};
Curve Loop(12) = {16, 53, -34, -52};
Surface(12) = {12};
Curve Loop(13) = {17, 54, -35, -53};
Surface(13) = {13};
Curve Loop(14) = {18, 51, -36, -54};
Surface(14) = {14};

// Surface - Sides of grid
Curve Loop(15) = {1, 38, -19, -37};
Plane Surface(15) = {15};
Curve Loop(16) = {2, 39, -20, -38};
Surface(16) = {16};
Curve Loop(17) = {3, 40, -21, -39};
Surface(17) = {17};
Curve Loop(18) = {4, 41, -919, -907, -22, -40};
Plane Surface(18) = {18};

Curve Loop(19) = {5, 42, -23, -41};
Plane Surface(19) = {19};

Curve Loop(20) = {6, 43, -24, -42};
Plane Surface(20) = {20};
Curve Loop(21) = {7, 44, -25, -43};
Surface(21) = {21};
Curve Loop(22) = {8, 45, -26, -44};
Surface(22) = {22};
Curve Loop(23) = {9, 46, -920, -910, -27, -45};
Plane Surface(23) = {23};

Curve Loop(24) = {10, 37, -28, -46};
Plane Surface(24) = {24};

// Volume - Grid
Surface Loop(201) = {3, 903, 904, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24};
Volume(1) = {201};


//**********************************************************************
// PADS & DIELECTRIC
//**********************************************************************

// ***** PADS *****

// Points - Central Pad
Point(101) = {0, 0, padBase, padElm};
Point(102) = {padLength, 0, padBase, padElm};
Point(103) = {halfPad, padMath, padBase, padElm};
Point(104) = {0, padMath, padBase, padElm};

// Lines - Central Pad
Line(101) = {101, 102};
Line(102) = {102, 103};
Line(103) = {103, 104};
Line(104) = {104, 101};

// Points - Corner Pad
Point(105) = {xMax, yMax, padBase, padElm};
Point(106) = {xMax - padLength, yMax, padBase, padElm};
Point(107) = {xMax - halfPad, yMax - padMath, padBase, padElm};
Point(108) = {xMax, yMax - padMath, padBase, padElm};

// Lines - Corner Pad
Line(105) = {105, 106};
Line(106) = {106, 107};
Line(107) = {107, 108};
Line(108) = {108, 105};

// Surfaces - Pads
Curve Loop(121) = {101, 102, 103, 104};
Plane Surface(121) = {121};
Curve Loop(122) = {105, 106, 107, 108};
Plane Surface(122) = {122};


// ***** Dielectric *****

// Points - Dielectric Bottom
Point(109) = {xMax, 0, padBase, diElm};
Point(110) = {0, yMax, padBase, diElm};

// Lines - Dielectric Bottom
Line(109) = {102, 109};
Line(110) = {109, 108};
Line(111) = {106, 110};
Line(112) = {110, 104};

// Points - Dielectric Top
Point(111) = {0, 0, topSiO2, padElm};
Point(112) = {padLength, 0, topSiO2, padElm};
Point(113) = {halfPad, padMath, topSiO2, padElm};
Point(114) = {0, padMath, topSiO2, padElm};
Point(115) = {xMax, yMax, topSiO2, padElm};
Point(116) = {xMax - padLength, yMax, topSiO2, padElm};
Point(117) = {xMax - halfPad, yMax - padMath, topSiO2, padElm};
Point(118) = {xMax, yMax - padMath, topSiO2, padElm};
Point(119) = {xMax, 0, topSiO2, diElm};
Point(120) = {0, yMax, topSiO2, diElm};

// Lines - Dielectric Top
Line(113) = {112, 903};
Line(921) = {902, 119};
Line(114) = {119, 118};
Line(115) = {118, 117};
Line(116) = {117, 116};
Line(117) = {116, 906};
Line(922) = {907, 120};
Line(118) = {120, 114};
Line(119) = {114, 113};
Line(120) = {113, 112};

// Connect - Top and Bottom of Dielectric
Line(121) = {112, 102};
Line(122) = {113, 103};
Line(123) = {114, 104};
Line(124) = {116, 106};
Line(125) = {117, 107};
Line(126) = {118, 108};
Line(127) = {119, 109};
Line(128) = {120, 110};

Line(129) = {111, 101};
Line(130) = {115, 105};

//Surface - top of dielectric
Curve Loop(123) = {113, -903, -902, 921, 114, 115, 116, 117, -906, -905, 922, 118, 119, 120};
Plane Surface(123) = {123};

//Surface - bottom of dielectric
Curve Loop(124) = {109, 110, -107, -106, 111, 112, -103, -102};
Plane Surface(124) = {124};

//Surface - Sides of dielectric
Curve Loop(125) = {113, 901, 921, 127, -109, -121};
Plane Surface(125) = {125};
Curve Loop(126) = {114, 126, -110, -127};
Plane Surface(126) = {126};
Curve Loop(127) = {115, 125, 107, -126};
Plane Surface(127) = {127};
Curve Loop(128) = {116, 124, 106, -125};
Plane Surface(128) = {128};
Curve Loop(129) = {117, 904, 922, 128, -111, -124};
Plane Surface(129) = {129};
Curve Loop(130) = {118, 123, -112, -128};
Plane Surface(130) = {130};
Curve Loop(131) = {119, 122, 103, -123};
Plane Surface(131) = {131};
Curve Loop(132) = {120, 121, 102, -122};
Plane Surface(132) = {132};

// Volume - Dielectric
Surface Loop(202) = {123, 901, 902, 124, 125, 126, 127, 128, 129, 130, 131, 132};
Volume(101) = {202};


//**********************************************************************
// GAS VOLUME
//**********************************************************************

// Points - Cathode Plane
Point(301) = {0, 0, cathodeDistance, cathElm};
Point(302) = {xMax, 0, cathodeDistance, cathElm};
Point(303) = {xMax, halfPitch, cathodeDistance, cathElm};
Point(304) = {0, halfPitch, cathodeDistance, cathElm};

// Lines - Cathode Plane
Line(301) = {301, 302};
Line(302) = {302, 303};
Line(303) = {303, 304};
Line(304) = {304, 301};

// Vertical lines - Gas Corner through holes
Line(305) = {301, 1};
Line(306) = {23, 111};
Line(307) = {303, 7};
Line(308) = {29, 115};

// Vertical lines - Gas Corner with grid
Line(309) = {302, 6};
Line(310) = {28, 119};
Line(311) = {304, 12};
Line(312) = {34, 120};

// Surface - Top Gas (Cathode)
Curve Loop(321) = {301, 302, 303, 304};
Plane Surface(321) = {321};

// Surfaces - Gas Sides
Curve Loop(322) = {301, 309, -4, 40, 22, 914, -113, 121, -101, -129, -306, 19, -38, -1, -305};
Plane Surface(322) = {322};
Curve Loop(925) = {919, 310, -921, -913};
Plane Surface(925) = {925};

Curve Loop(323) = {302, 307, -5, -309};
Plane Surface(323) = {323};
Curve Loop(327) = {23, 308, 130, -108, -126, -114, -310};
Plane Surface(327) = {327};

Curve Loop(324) = {303, 311, -9, 45, 27, 916, -117, 124, -105, -130, -308, 24, -43, -6, -307};
Plane Surface(324) = {324};
Curve Loop(926) = {920, 312, -922, -918};
Plane Surface(926) = {926};

Curve Loop(325) = {304, 305, -10, -311};
Plane Surface(325) = {325};
Curve Loop(326) = {28, 306, 129, -104, -123, -118, -312};
Plane Surface(326) = {326};

// Volume - Gas
Surface Loop(203) = {
    321, //Cathode
    322, 323, 324, 325, 326, 327, 925, 926, //Sides of volume
    3, 6, 7, 8, 9, 10, 11, 12, 13, 14, 16, 17, 21, 22, //Grid elements
    123, 127, 128, 131, 132, //dielectric
    121, 122, // Pad elements
    906, 907, 909, 910// Pillar elements
};
Volume(1001) = {203};


//**********************************************************************
// PHYSICAL GROUPS
//**********************************************************************

// Physical Volumes
Physical Volume("Grid") = {1};
Physical Volume("Dielectric") = {101};
Physical Volume("Gas") = {1001};
Physical Volume("LowerPillar") = {901};
Physical Volume("UpperPillar") = {902};


// ***** Physical Surfaces ***** //

// *** Constant Elements *** //

Physical Surface("Cathode") = {
    321
};

Physical Surface("Grid") = {
    3, // Top
    6, // Bottom
    //Sides 
    7, 8, 9, 10, 11, 12, 13, 14, // Holes
    15, 16, 17, 18, 19, 20, 21, 22, 23, 24 //Outer
};

Physical Surface("CentralPad") = {
    121
};

Physical Surface("SurroundingPads") = {
    122
};

Physical Surface("DielectricTop") = {
    123, // Top
    131, 132, // Central pad boundary
    127, 128 // Surrounding pad boundary
};

Physical Surface("Pillars") = {
    906, 907, // Lower pillar
    909, 910 // Upper pillar
};

// *** Boundary Elements *** //

Physical Surface("Boundaries") = {
    15, 18, 19, 20, 23, 24, //Grid
    322, 323, 324, 325, 326, 327, 925, 926, //Gas
    125, 126, 129, 130, // Dielectric
    905, 908 // Pillars
};

Coherence;
