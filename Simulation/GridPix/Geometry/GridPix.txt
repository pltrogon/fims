//**********************************************************************
// Gridpix geometry
//**********************************************************************

// Parameters in micrometers
pitch = 55;
gridThickness = 1;
cathodeHeight = 200;
gridStandoff = 50;
holeRadius = 17.5;
thicknessSiO2 = 4;
padLength = 20;
/*
Dimensions taken from:
    Performance of the GridPix detector quad
    C. Ligtenberg, Y. Bilevych, K. Desch, H. van der Graaf, et al.

    This also has a grid voltage of 350 V and a drift field of 280 V/cm.
        Field ratio ~69.3 (no grid thick =70)
        Cathode V (at 200um) ~-355.586 (no grid thick =-355.6)

    Gas is: T2K TPC (95 Ar, 3 CF4, 2 iC4H10)
        Temp = 301.63 +/- 0.08 K
        P = 1034.20 +/- 0.05 mbar
        oxy concentration 211 ppm

Note that the pad is an octagon with diameter 20. See:
    Timepix, a 65k programmable pixel readout chip for arrival time, energy and/or photon counting measurements
    X. Llopart, R. Ballabriga, M. Campbell, L. Tlustos, W. Wong
*/


halfPitch = pitch/2;
halfPad = padLength/2;
sqrt2 = 2^.5;
padMath = halfPad/(1+sqrt2);

// Finite element sizes for different regions
gridElm = (pitch - holeRadius)/5;
holeElm = gridThickness;
padElm = thicknessSiO2;
diElm = padLength/4;
cathElm = pitch/10;

// Derived Z-coordinates for different layers
//     Center of grid: z = 0
cathodeDistance = gridThickness/2 + cathodeHeight;
gridTop = gridThickness/2;
gridBottom = -gridThickness/2;
topSiO2 = - gridThickness/2 - gridStandoff + thicknessSiO2;
padBase = -gridThickness/2 - gridStandoff;

// Global coordinate limits
xMin = 0;
xMax = halfPitch;
yMin = 0;
yMax = halfPitch;

//**********************************************************************
// GRID STRUCTURE
//**********************************************************************

// Points - top of the grid
Point(1) = {0, 0, gridTop, holeElm};
Point(2) = {0, holeRadius, gridTop, holeElm};
Point(3) = {holeRadius, 0, gridTop, holeElm};

Point(4) = {0, yMax, gridTop, gridElm};
Point(5) = {xMax, 0, gridTop, gridElm};
Point(6) = {xMax, yMax, gridTop, gridElm};

// Points - bottom of the grid
Point(7) = {0, 0, gridBottom, holeElm};
Point(8) = {0, holeRadius, gridBottom, holeElm};
Point(9) = {holeRadius, 0, gridBottom, holeElm};

Point(10) = {0, yMax, gridBottom, gridElm};
Point(11) = {xMax, 0, gridBottom, gridElm};
Point(12) = {xMax, yMax, gridBottom, gridElm};

// Curves - top of grid
Circle(101) = {2, 1, 3};
Line(102) = {2, 4};
Line(103) = {3, 5};
Line(104) = {4, 6};
Line(105) = {5, 6};

// Curves - boottom of grid
Circle(106) = {8, 7, 9};
Line(107) = {8, 10};
Line(108) = {9, 11};
Line(109) = {10, 12};
Line(110) = {11, 12};

//Connect top and bottom
Line(111) = {1, 7};
Line(112) = {2, 8};
Line(113) = {3, 9};
Line(114) = {4, 10};
Line(115) = {5, 11};
Line(116) = {6, 12};

//Surfaces
//top
Curve Loop(201) = {101, 103, 105, -104, -102};
Plane Surface(301) = {201};
//bottom
Curve Loop(202) = {106, 108, 110, -109, -107};
Plane Surface(302) = {202};
// Sides
//Hole
Curve Loop(203) = {101, 113, -106, -112};
Surface(303) = {203};
//top
Curve Loop(204) = {104, 116, -109, -114};
Plane Surface(304) = {204};
//bottom
Curve Loop(205) = {103, 115, -108, -113};
Plane Surface(305) = {205};
//left
Curve Loop(206) = {102, 114, -107, -112};
Plane Surface(306) = {206};
//right
Curve Loop(207) = {105, 116, -110, -115};
Plane Surface(307) = {207};


// ***** Grid Volume ***** //
Surface Loop(401) = {
    301, // Top
    302, // Bottom
    303, 304, 305, 306, 307 //Sides
};
Volume(1) = {401};


//**********************************************************************
// Pads and Dielectric
//**********************************************************************

// ***** PAD ***** //
//Points
Point(13) = {0, 0, padBase, padElm};
Point(14) = {0, halfPad, padBase, padElm};
Point(15) = {halfPad, 0, padBase, padElm};
Point(16) = {halfPad, padMath, padBase, padElm};
Point(17) = {padMath, halfPad, padBase, padElm};

//Lines
Line(117) = {13, 14};
Line(118) = {13, 15};
Line(119) = {14, 17};
Line(120) = {15, 16};
Line(121) = {16, 17};

//Surface
Curve Loop(208) = {117, 119, -121, -120, -118};
Plane Surface(308) = {208};


// ***** Dielectric *****
//points - Dielectric bottom
Point(18) = {xMax, 0, padBase, diElm};
Point(19) = {0, yMax, padBase, diElm};
Point(20) = {xMax, yMax, padBase, diElm};

//lines - dielectric bottom
Line(122) = {14, 19};
Line(123) = {15, 18};
Line(124) = {18, 20};
Line(125) = {19, 20};

//points - Dielectric top
Point(21) = {0, halfPad, topSiO2, padElm};
Point(22) = {halfPad, 0, topSiO2, padElm};
Point(23) = {halfPad, padMath, topSiO2, padElm};
Point(24) = {padMath, halfPad, topSiO2, padElm};
Point(25) = {0, yMax, topSiO2, diElm};
Point(26) = {xMax, 0, topSiO2, diElm};
Point(27) = {xMax, yMax, topSiO2, diElm};

//lines - dielectric top
Line(126) = {21, 24};
Line(127) = {22, 23};
Line(128) = {23, 24};
Line(129) = {21, 25};
Line(130) = {22, 26};
Line(131) = {25, 27};
Line(132) = {26, 27};

//lines - connect top and bottom
Line(133) = {21, 14};
Line(134) = {22, 15};
Line(135) = {23, 16};
Line(136) = {24, 17};
Line(137) = {25, 19};
Line(138) = {26, 18};
Line(139) = {27, 20};

//Surfaces
//top
Curve Loop(209) = {129, 131, -132, -130, 127, 128, -126};
Plane Surface(309) = {209};
//bottom
Curve Loop(210) = {122, 125, -124, -123, 120, 121, -119};
Plane Surface(310) = {210};
//sides
Curve Loop(211) = {132, 139, -124, -138};//top
Plane Surface(311) = {211};
Curve Loop(212) = {129, 137, -122, -133};//bottom
Plane Surface(312) = {212};
Curve Loop(213) = {130, 138, -123, -134};//left
Plane Surface(313) = {213};
Curve Loop(214) = {131, 139, -125, -137};//right
Plane Surface(314) = {214};
Curve Loop(215) = {127, 135, -120, -134};//padtop
Plane Surface(315) = {215};
Curve Loop(216) = {126, 136, -119, -133};//padright
Plane Surface(316) = {216};
Curve Loop(217) = {128, 136, -121, -135};//padAngle
Plane Surface(317) = {217};

// ***** Dielectric Volume ***** //
Surface Loop(402) = {
    309, // Top
    310, // Bottom
    311, 312, 313, 314, 315, 316, 317 //Sides
};
Volume(2) = {402};


//**********************************************************************
// GAS VOLUME
//**********************************************************************

//Points - Cathode plane
Point(28) = {0, 0, cathodeDistance, cathElm};
Point(29) = {0, yMax, cathodeDistance, cathElm};
Point(30) = {xMax, 0, cathodeDistance, cathElm};
Point(31) = {xMax, yMax, cathodeDistance, cathElm};

//Lines - cathode plane
Line(140) = {28, 29};
Line(141) = {28, 30};
Line(142) = {29, 31};
Line(143) = {30, 31};

// Surface - Top Gas (Cathode)
Curve Loop(218) = {140, 142, -143, -141};
Plane Surface(318) = {218};

//Lines
//cathode to grid
Line(144) = {28, 1};
Line(145) = {29, 4};
Line(146) = {30, 5};
Line(147) = {31, 6};

//grid to base
Line(148) = {7, 13};
Line(149) = {10, 25};
Line(150) = {11, 26};
Line(151) = {12, 27};

// Surfaces - Gas Sides
Curve Loop(219) = {//top-1
    142, 147, -104, -145
};
Plane Surface(319) = {219};
Curve Loop(220) = {//top-2
    109, 151, -131, -149
};
Plane Surface(320) = {220};
Curve Loop(221) = {//right-1
    143, 147, -105, -146
};
Plane Surface(321) = {221};
Curve Loop(222) = {//right-2
    110, 151, -132, -150
};
Plane Surface(322) = {222};
Curve Loop(223) = {//bottom
    141, 
    146, -103, 113, 108, 150,
    -130, 134, -118,
    -148, -111, -144
};
Plane Surface(323) = {223};
Curve Loop(224) = {//left
    140,
    145, -102, 112, 107, 149,
    -129, 133, -117,
    -148, -111, -144
};
Plane Surface(324) = {224};

// ***** Gas Volume ***** //
Surface Loop(403) = {
    318, // Cathode
    319, 320, 321, 322, 323, 324, //Sides of volume
    301, 302, 303, // Grid elements
    309, 315, 316, 317, // Dielectric
    308 //Pad
};
Volume(3) = {403};


//**********************************************************************
// PHYSICAL GROUPS
//**********************************************************************

// Physical Volumes
Physical Volume("Grid") = {1};
Physical Volume("Dielectric") = {2};
Physical Volume("Gas") = {3};


// ***** Physical Surfaces ***** //

// *** Constant Elements *** //

Physical Surface("Cathode") = {
    318
};

Physical Surface("Grid") = {
    301, // Top
    302, // Bottom
    //Sides
    303, // Hole
    304, 305, 306, 307 // sides
};

Physical Surface("Pad") = {
    308
};

Physical Surface("DielectricTop") = {
    309
};

// *** Boundary Elements *** //

Physical Surface("Boundaries") = {
    319, 320, 321, 322, 323, 324, //Gas
    311, 312, 313, 314, // Dielectric
    304, 305, 306, 307 //Grid
};

Coherence;